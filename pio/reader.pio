.program reader
.clock_div 2.3 
; Account for overclocking

.wrap_target

wait 0 pin 23 ; ensure the PWM clock is down to sync
wait 1 pin 23 ; start acquisition on rising edge of the PWM clock

; --- IDLE ---
set pins, 0b111        ; RD=1, CONVST=1, CS=1
nop [3]                ; 5 cycles = 40 ns

; --- START CONVERSION ---
set pins, 0b110        ; RD=1, CONVST=1, CS=0
nop [2]                ; 3 cycles = 24 ns
set pins, 0b100        ; RD=1, CONVST=0, CS=0
nop [4]                ; 5 cycles = 40 ns ≥ tw1
set pins, 0b110        ; RD=1, CONVST=1, CS=0
nop [4]                ; 5 cycles = 40 ns ≥ tw2

; --- WAIT FOR BUSY LOW (~600 ns) ---
wait 0 pin 16          ; waits for BUSY to drop

; --- READ DATA ---
set pins, 0b010        ; RD=0, CONVST=1, CS=0
nop [6]                ; 7 cycles = 56 ns ≥ tw5
in pins, 16            ; sample DB[15:0] : the autopush will handle the data

; --- RETURN TO IDLE ---
set pins, 0b111        ; RD=1, CONVST=1, CS=1
nop [4]                ; 5 cycles = 40 ns ≥ CS high before next cycle

.wrap